
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТекущийОстатокДенежныхСредств = ПолучитьОстаткиДенежныхСредств();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьОстатокЛимитаДенежныхСредств();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененОстатокДенежныхСредств" Тогда
		ТекущийОстатокДенежныхСредств = ПолучитьОстаткиДенежныхСредств();
	ИначеЕсли ИмяСобытия = "ИзмененОстатокЛимитаДенежныхСредств" Тогда
		СформироватьОстатокЛимитаДенежныхСредств();	
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФактическийОстатокЛимитаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВидРасхода", ПредопределенноеЗначение("Перечисление.ВидыРасходов.Фактический"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоДате", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаСписка", ПараметрыФормы);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыйОстатокЛимитаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВидРасхода", ПредопределенноеЗначение("Перечисление.ВидыРасходов.Планируемый"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоДате", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьОстатокЛимитаДенежныхСредств(Команда)

	СформироватьОстатокЛимитаДенежныхСредств();	

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущийОстатокДенежныхСредств(Команда)

	ТекущийОстатокДенежныхСредств = ПолучитьОстаткиДенежныхСредств();	

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПриход(Команда)
	
	ОткрытьФорму("Документ.ПриходДенежныхСредств.Форма.ФормаДокумента");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаДокумента");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПланируемыйРасход(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидРасхода", ПредопределенноеЗначение("Перечисление.ВидыРасходов.Планируемый"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция ПолучитьОстаткиДенежныхСредств()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиДенежныхСредствОстатки.СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства.Остатки КАК ОстаткиДенежныхСредствОстатки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.СуммаОстаток;
	
КонецФункции

&НаСервере
Функция ПолучитьФактическийОстатокЛимитаДенежныхСредств()

	ЛимитДС = Константы.ЛимитДенежныхСредств.Получить(); 

	ДатаНачалаНедели = ОбщегоНазначенияКлиентСервер.ПолучитьДатуНачалаПериода(ТекущаяДата());

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиДенежныхСредствОбороты.СуммаРасход КАК СуммаРасход
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства.Обороты(&ДатаНачала, , , ) КАК ОстаткиДенежныхСредствОбороты";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачалаНедели);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Если ВыборкаДетальныеЗаписи.СуммаРасход = Неопределено Тогда
		СуммаРасход = 0;
	Иначе
		СуммаРасход = ВыборкаДетальныеЗаписи.СуммаРасход;
	КонецЕсли;
	
	Возврат ЛимитДС - СуммаРасход;
	
КонецФункции

&НаСервере
Функция ПолучитьПланируемыйОстатокЛимитаДенежныхСредств()

	ДатаНачалаНедели = ОбщегоНазначенияКлиентСервер.ПолучитьДатуНачалаПериода(ТекущаяДата());
	ДатаОкончанияНедели = ОбщегоНазначенияКлиентСервер.ПолучитьДатуОкончанияПериода(ТекущаяДата());

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПланируемыеОстаткиДенежныхСредствОбороты.СуммаОборот КАК СуммаОборот
		|ИЗ
		|	РегистрНакопления.ПланируемыеОстаткиДенежныхСредств.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ПланируемыеОстаткиДенежныхСредствОбороты";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачалаНедели);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончанияНедели);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Если ВыборкаДетальныеЗаписи.СуммаОборот = Неопределено Тогда
		СуммаОборот = 0;
	Иначе
		СуммаОборот = ВыборкаДетальныеЗаписи.СуммаОборот;
	КонецЕсли;
	
	Возврат СуммаОборот;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОстатокЛимитаДенежныхСредств()
	
	ФактическийОстатокЛимита = ПолучитьФактическийОстатокЛимитаДенежныхСредств();
	ПланируемыеРасходы = ПолучитьПланируемыйОстатокЛимитаДенежныхСредств();
	
	ПлановыйОстатокЛимита = ФактическийОстатокЛимита - ПланируемыеРасходы;
	
	//ОстатокЛимитаДенежныхСредств = "Факт. " + ФактическийОстаток + " План. " + ПланируемыйОстаток;
	
КонецПроцедуры

#КонецОбласти

