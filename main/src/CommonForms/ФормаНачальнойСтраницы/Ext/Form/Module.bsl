
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Заголовок = Метаданные.Синоним;
	
	ИспользуемаяДата = ОбщегоНазначенияСервер.ПолучитьИспользуемуюДату();
	
	ПлановыеРасходыНаТекущийПериод.Параметры.УстановитьЗначениеПараметра("ДатаНачала",
		ОбщегоНазначенияКлиентСервер.ПолучитьДатуНачалаПериода(ИспользуемаяДата));
		
	ПлановыеРасходыНаТекущийПериод.Параметры.УстановитьЗначениеПараметра("ДатаОкончания",
		ОбщегоНазначенияКлиентСервер.ПолучитьДатуОкончанияПериода(ИспользуемаяДата));	
	
	ПересчитатьОстатки();	
	
	РассчитатьИтоговуюСуммуПоСпискуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеОстатков" Тогда
		ПересчитатьОстатки();
		РассчитатьИтоговуюСуммуПоСпискуДокументов();
	ИначеЕсли ИмяСобытия = "ИзменениеЛимитаНаПериод" Тогда
		ПересчитатьОстаткиПриИзмененииЛимита();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФактическийОстатокЛимитаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УчетДенежныхСредствКлиент.ОткрытьФормуСпискаФактическихРасходов();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыйОстатокЛимитаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УчетДенежныхСредствКлиент.ОткрытьФормуСпискаПланируемыхРасходов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыНаТекущийПериодПослеУдаления(Элемент)
	
	РассчитатьИтоговуюСуммуПоСпискуДокументов();
	Оповестить("ИзменениеОстатков");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьТекущийОстатокДенежныхСредств(Команда)

	ПересчитатьОстатки();	

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПриход(Команда)
	
	ОткрытьФорму("Документ.ПриходДенежныхСредств.Форма.ФормаДокумента");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаДокумента");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПланируемыйРасход(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидРасхода", ПредопределенноеЗначение("Перечисление.ВидыРасходов.Планируемый"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.РасходДенежныхСредств.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФактическийОстатокЛимита", ФактическийОстатокЛимита);
	
	ОткрытьФорму("ОбщаяФорма.ФормаНастроек", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ПересчитатьОстатки()
	
	ТекущийОстатокДенежныхСредств = УчетДенежныхСредствСервер.ПолучитьОстатокДенежныхСредств();
	ФактическийОстатокЛимита = УчетДенежныхСредствСервер.РассчитатьФактическийОстатокЛимитаДенежныхСредствНаПериод();
	ПланируемыеРасходы = УчетДенежныхСредствСервер.ПолучитьПланируемыеРасходыЗаПериод();

	ПлановыйОстатокЛимита = ФактическийОстатокЛимита - ПланируемыеРасходы;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьОстаткиПриИзмененииЛимита()
	
	ФактическийОстатокЛимита = УчетДенежныхСредствСервер.ПолучитьОстатокЛимитаПриИзмененииЛимитаНаПериод();
	ПланируемыеРасходы = УчетДенежныхСредствСервер.ПолучитьПланируемыеРасходыЗаПериод();

	ПлановыйОстатокЛимита = ФактическийОстатокЛимита - ПланируемыеРасходы;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуПоСпискуДокументов()
	
	МассивВидовРасхода = Новый Массив;
	МассивВидовРасхода.Добавить(Перечисления.ВидыРасходов.Планируемый);
	
	ИспользуемаяДата = ОбщегоНазначенияСервер.ПолучитьИспользуемуюДату();
	ДатаНачалаПериода = ОбщегоНазначенияКлиентСервер.ПолучитьДатуНачалаПериода(ИспользуемаяДата);
	ДатаОкончанияПериода = ОбщегоНазначенияКлиентСервер.ПолучитьДатуОкончанияПериода(ИспользуемаяДата);
		
	ТекстЗапрса =  
		"ВЫБРАТЬ
		|	СУММА(РасходДенежныхСредств.Сумма) КАК Сумма
		|ИЗ
		|	Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
		|ГДЕ
		|	РасходДенежныхСредств.ВидРасхода В(&МассивВидовРасхода)
		|	И РасходДенежныхСредств.Дата МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапрса;
	Запрос.УстановитьПараметр("МассивВидовРасхода", МассивВидовРасхода);
	Запрос.УстановитьПараметр("ДатаНачалаПериода", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода", КонецДня(ДатаОкончанияПериода));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ИтоговаяСумма = ВыборкаДетальныеЗаписи.Сумма;
	
	Если ИтоговаяСумма <> 0 Тогда
		Элементы.Декорация1.Заголовок = "План на текущую неделю на сумму " + ИтоговаяСумма;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

