
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФактическийОстатокЛимита = Параметры.ФактическийОстатокЛимита;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьФактическийОстатокЛимита(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФактическийОстатокЛимита", ФактическийОстатокЛимита);
	
	ОткрытьФорму("РегистрСведений.ОстатокЛимитаДенежныхСредств.Форма.ФормаРедактированияЛимита", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЛимитНаПериод(Команда)
	
	ОткрытьФорму("РегистрСведений.ЛимитДенежныхСредств.Форма.ФормаУстановкиЛимитаНаПериод", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВсеДанные(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьВсеДанныеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, 
		НСтр("ru = 'Будут удалены все данные приложения. Продолжить?'"), 
		РежимДиалогаВопрос.ДаНет, 
		60,
		КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УдалитьВсеДанныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьВсеДанныеСервер();
		Оповестить("ИзменениеОстатков");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВсеДанныеСервер()

	// Документы
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходДенежныхСредств.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПриходДенежныхСредств КАК ПриходДенежныхСредств
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходДенежныхСредств.Ссылка
		|ИЗ
		|	Документ.РасходДенежныхСредств КАК РасходДенежныхСредств";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДокумент = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДокумент.Следующий() Цикл
		ДокОбъект = ВыборкаДокумент.Ссылка.ПолучитьОбъект();
		ДокОбъект.Удалить();
	КонецЦикла;
	
	// Константы
	Константы.ПоследняяДатаРасчетаФактическогоОстаткаЛимита.Установить(Дата("00010101"));
	
	// РегистрыСведений
	НаборЗаписей = РегистрыСведений.ЛимитДенежныхСредств.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	НаборЗаписей = РегистрыСведений.ОстатокЛимитаДенежныхСредств.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти