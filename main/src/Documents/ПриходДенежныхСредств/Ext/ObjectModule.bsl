
#Область ОбработчикиСобытийОбъекта

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Движения.ДенежныеСредства.Записывать = Истина;
	
	Движение = Движения.ДенежныеСредства.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Сумма = Сумма;
	
	СформироватьДвиженияПоРегиструОстатокЛимитаДенежныхСредств();	

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура СформироватьДвиженияПоРегиструОстатокЛимитаДенежныхСредств()
	
	СуммаДоНовогоЛимита = РассчитатьСуммуДляЛимита();
	
	Если СуммаДоНовогоЛимита = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ОстатокЛимитаДенежныхСредств.Записывать = Истина;
	
	Движение = Движения.ОстатокЛимитаДенежныхСредств.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Сумма = СуммаДоНовогоЛимита;
	
КонецПроцедуры

Функция РассчитатьСуммуДляЛимита()
	
	ТекущийОстатокЛимита = УчетДенежныхСредствСервер.ПолучитьОстатокЛимитаДенежныхСредств();
	ЛимитДенежныхСредств = УчетДенежныхСредствСервер.ПолучитьЛимит();
	РасходыЗаТекущийПериод = УчетДенежныхСредствСервер.ПолучитьРасходыЗаТекущийПериод();
	
	ОсталосьОтЛимита = ЛимитДенежныхСредств - (ТекущийОстатокЛимита + РасходыЗаТекущийПериод);
	
	СуммаДляРаспределения = Мин(ОсталосьОтЛимита, Сумма);
	
	Если СуммаДляРаспределения <= 0 Тогда
		СуммаДоНовогоЛимита = 0;
	Иначе
		СуммаДоНовогоЛимита = УчетДенежныхСредствСервер.ПолучитьСуммуПоОставшимсяДням(СуммаДляРаспределения, Дата);
	КонецЕсли;
		
	Возврат СуммаДоНовогоЛимита;
	
КонецФункции

#КонецОбласти
