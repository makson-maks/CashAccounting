
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		Заголовок = "Приход - новый";
	Иначе
		Заголовок = "Приход";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьСуммуДоНовогоЛимита();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьЗаписьДокумента();
		
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСуммуДоНовогоЛимита(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаНовойСуммыДоНовогоЛимита", ЭтотОбъект);
	
	ПоказатьВводЧисла(ОписаниеОповещения, Объект.СуммаДоНовогоЛимита, "Введите сумму поступлений на текущую неделю", 10, 0);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ДатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительнаяОбработкаОповещения = Новый ОписаниеОповещения("ДатаНачалоВыбораЗавершение", ЭтотОбъект);
	
	УчетДенежныхСредствКлиент.ОткрытьФормуВыбораДаты(ЭтаФорма,,,,ДополнительнаяОбработкаОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	РассчитатьСуммуДоНовогоЛимита();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРаспределенияСуммыПриИзменении(Элемент)
	
	РассчитатьСуммуДоНовогоЛимита();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура НачатьЗаписьДокумента()
	
	ВыполнитьПроведениеДокумента();
	Оповестить("ИзменениеОстатков");
	ОповеститьОбИзменении(Объект.Ссылка);
	Закрыть();

КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроведениеДокумента()
	
	Модифицированность = Ложь;
	
	ОбъектДокумента = РеквизитФормыВЗначение("Объект");
	ОбъектДокумента.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
	ЗначениеВРеквизитФормы(ОбъектДокумента, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДоНовогоЛимита()
	
	Если ЗначениеЗаполнено(Объект.Сумма) Тогда
		
		Объект.СуммаДоНовогоЛимита = РассчитатьСуммуДоНовогоЛимитаНаСервере(
			Объект.Сумма, Объект.Дата, Объект.ВидРаспределенияСуммы);
		
	КонецЕсли;
		
	ВывестиСуммуДоНовогоЛимита();
		
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСуммуДоНовогоЛимита()
	
	ТекстЗаголовка = НСтр("ru = 'На текущую неделю к факт. лимиту будет добавлено %1 руб.'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
	Объект.СуммаДоНовогоЛимита);
	
	Элементы.Декорация1.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьСуммуДоНовогоЛимитаНаСервере(Сумма, Дата, ВидРаспределенияСуммы)
	
	Возврат УчетДенежныхСредствСервер.РассчитатьСуммуДоНовогоЛимита(Сумма, Дата, ВидРаспределенияСуммы);
	
КонецФункции

&НаКлиенте
Процедура ДатаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РассчитатьСуммуДоНовогоЛимита();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНовойСуммыДоНовогоЛимита(ВведенноеЧисло, Параметры) Экспорт
	
	Если ВведенноеЧисло <> Неопределено Тогда
		Если ВведенноеЧисло > Объект.Сумма Тогда
			ТекстСообщения = НСтр("ru = 'Вы преувеличили фактическую сумму поступления.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			Объект.СуммаДоНовогоЛимита = ВведенноеЧисло;
			ВывестиСуммуДоНовогоЛимита();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти