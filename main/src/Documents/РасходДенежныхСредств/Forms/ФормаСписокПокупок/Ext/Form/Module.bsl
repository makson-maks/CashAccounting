
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Таблица = ПолучитьИзВременногоХранилища(Параметры.СписокПокупок);
	СписокПокупок.Загрузить(Таблица);
	
	СохранятьСписокПокупок = Истина;
	
КонецПроцедуры              

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьИтоговыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если СохранятьСписокПокупок Тогда
		ОповеститьОВыборе(ПоместитьСписокПокупокВоВременноеХранилище());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	СохранятьСписокПокупок = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПокупокКоличествоПриИзменении(Элемент)
	
	РассчитатьСумму();
	РассчитатьИтоговыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПокупокЦенаПриИзменении(Элемент)
	
	РассчитатьСумму();
	РассчитатьИтоговыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПокупокКупленоПриИзменении(Элемент)
	
	РассчитатьИтоговыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПокупокТоварПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокПокупок.ТекущиеДанные;
	Если ТекущиеДанные.Количество = 0 Тогда
		ТекущиеДанные.Количество = 1;	
	КонецЕсли;
	
	ТекущиеДанные.Цена = ПолучитьЦенуНоменклатуры(ТекущиеДанные.Товар);
	
	РассчитатьСумму();
	РассчитатьИтоговыеПоказатели();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Функция ПоместитьСписокПокупокВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(СписокПокупок.Выгрузить());

КонецФункции

&НаКлиенте
Процедура РассчитатьСумму()
	
	ТекущиеДанные = Элементы.СписокПокупок.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Количество * ТекущиеДанные.Цена;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦенуНоменклатуры(Номенклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатуры.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|ГДЕ
		|	ЦеныНоменклатуры.Номенклатура = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Цена;
	
КонецФункции

&НаКлиенте
Процедура РассчитатьИтоговыеПоказатели()
	
	ИтогоЗапланировано = 0;
	ИтогоКуплено = 0;
	
	Для каждого СтрокаТЧ Из СписокПокупок Цикл
		
		ИтогоЗапланировано = ИтогоЗапланировано + СтрокаТЧ.Сумма;
		
		Если СтрокаТЧ.Куплено Тогда
			ИтогоКуплено = ИтогоКуплено + СтрокаТЧ.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти